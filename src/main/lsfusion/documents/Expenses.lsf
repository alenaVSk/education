MODULE Expenses;
// 1. Необходимые для работы модули
REQUIRE Documents, AppMenu, Organization, Reception, Utils;
// 2. Задаем общее пространство имен для всех документов
NAMESPACE Documents;
// 3. Наследуем класс приходы от класса Document
CLASS Expenses 'Расходы':Document;
// 4. Добавляем свойства, которых нет в базовом классе
// так как товар и цена берутся из прихода, то нужна композиция
reception 'ID' = DATA Reception (Expenses) NONULL ;
name 'Наименование' (Expenses o) = name(reception(o));
price 'Цена' (Expenses o) = round(1.1 * price(reception(o)),2); // 1.1 - оптовая надбавка
// 4. Композиция
customer = DATA Organization (Expenses);
organizationName 'Покупатель' (Expenses o) = name(customer(o));
// 5. Контроль данных
allExpenses (Reception r) = GROUP SUM quantity(Expenses e) BY reception(e) MATERIALIZED;
balance 'Остаток' (Reception r) = quantity(r) (-) allExpenses(r);
CONSTRAINT balance(reception(Expenses o)) < 0 MESSAGE 'Расход превышает остаток';
CONSTRAINT SETCHANGED(customer(Expenses o)) AND NOT organizationType(customer(o)) =
    OrganizationType.customer
    CHECKED BY customer[Expenses] MESSAGE 'Для расходной накладной организация должна быть
покупателем';

@forms(Expenses, 'Расход', 'Расходы', 'Покупатель', 'Магазин'){
    // форма редактирования
    FORM editExpenses 'Расход'
        OBJECTS o = Expenses PANEL
        PROPERTIES (o) number, date, organizationName, name, quantity, price
        EDIT Expenses OBJECT o
    ;
    // форма отображения
    FORM viewExpenses 'Расходы'
        OBJECTS o = Expenses
        PROPERTIES (o) READONLY number, date, organizationName, name, quantity, price, userName, userDate
        PROPERTIES (o) NEWSESSION NEW, EDIT, DELETE
        ORDERS date(o), number(o)
    ;
    // дизайн формы    
    DESIGN editExpenses {
        OBJECTS {
            NEW cntDoc {
                horizontal = TRUE; // распределяем элементы слева-направо-сверху-вниз
                caption = 'Накладная';
                height = 100;
                MOVE PROPERTY (number(o));
                MOVE PROPERTY (date(o));
            }
            NEW cntOrg {
                horizontal = TRUE;
                caption = 'Покупатель';
                height = 100;
                MOVE PROPERTY (organizationName(o)) { caption = 'Магазин'; }
            }
            NEW cntRow {
                horizontal = FALSE ;
                caption = 'Строки накладной';
                MOVE PROPERTY (name(o));
                MOVE PROPERTY (quantity(o));
                MOVE PROPERTY (price(o));
            }
        }
    }
    NAVIGATOR {
        documents {
            NEW FORM viewExpenses;
        }
    }
};

