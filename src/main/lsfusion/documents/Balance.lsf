MODULE Balance;

REQUIRE Expenses, Group, AppMenu;
date 'Дата прихода' (Reception o) = date(header(o)) CHARWIDTH 10;
FORM viewBalance 'Журнал остатков'
    TREE groups g = Group PARENT parent(g)
    PROPERTIES READONLY name(g)
    OBJECTS r = Reception
    PROPERTIES (r) READONLY nameProduct, nameUnit, price, date, quantity, allExpenses, balance
    FILTERS level(group(product(r)),g)
;
DESIGN viewBalance {
    OBJECTS {
        NEW pane {
            fill = 1;
            horizontal = TRUE ;
            MOVE BOX(TREE groups) { fill=1; caption='Группы'; }
            MOVE BOX(r) { fill=20; caption='Остатки'; }
        }
    }
    PROPERTY (nameUnit(r)) { charWidth=10; }
    PROPERTY (quantity(r)) { caption='Приход'; charWidth=10; }
    PROPERTY (allExpenses(r)) { caption = 'Расход'; charWidth=10; }
    PROPERTY (balance(r)) { charWidth=10; }
}
NAVIGATOR {
    documents {
        NEW FORM viewBalance;
    }
}

nameGroup 'Группа' (Reception o) = name(group(product(o)));
organization 'ID организации' (Reception o) = supplier(header(o));
EXTEND FORM viewBalance
    OBJECTS o = Organization
    PROPERTIES (o) READONLY name
    OBJECTS r2 = Reception
    PROPERTIES (r2) READONLY nameProduct, nameUnit, nameGroup, price, date, quantity,
        allExpenses, balance
    FILTERS organizationType(o) = OrganizationType.supplier
    FILTERS organization(r2) = o
;
DESIGN viewBalance {
    pane { caption = 'Группы'; }
    OBJECTS {
        NEW tabbed {
            tabbed = TRUE; // Задает вкладки
            fill = 1;
            MOVE pane; // перемещаем контейнер дерева и остатков
            NEW pane2 { // контейнер организаций и остатков
                horizontal = TRUE;
                caption = 'Поставщики';
                MOVE BOX(o) { fill=1; caption='Поставщики'; }
                MOVE BOX(r2) { fill=20; caption='Остатки'; }
            }
        }
    }
    PROPERTY (nameUnit(r2)) { charWidth=10; }
    PROPERTY (quantity(r2)) { caption='Приход'; charWidth=10; }
    PROPERTY (allExpenses(r2)) { caption = 'Расход'; charWidth=10; }
    PROPERTY (balance(r2)) { charWidth=10; }
}