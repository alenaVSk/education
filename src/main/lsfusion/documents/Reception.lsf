MODULE Reception;
// 1. Необходимые для работы модули
REQUIRE Documents, AppMenu, Organization, Product, Unit;
// 2. Задаем общее пространство имен для всех документов
NAMESPACE Documents;
// 3. Наследуем класс заголовка прихода от класса Header
CLASS HeaderReception 'Приходы':Header;
// 3. Наследуем класс строки прихода от класса Row
CLASS Reception 'Строки приходов':Row;
// 4. Добавляем свойства, которых нет в базовом классе
price 'Цена' = DATA NUMERIC[10,2] (Reception) NONULL;
// 4. Композиция
supplier = DATA Organization (HeaderReception);
organizationName 'Поставщик' (HeaderReception o) = name(supplier(o));
// 4. Новые свойства:
product 'ID товара' = DATA Product (Reception) NONULL;
unit 'ID единиц измерения' = DATA Unit (Reception) NONULL;
nameProduct 'Наименование товара' (Reception o) = name(product(o));
nameUnit 'Ед. изм.' (Reception o) = name(unit(o));
// 5. Контроль данных
CONSTRAINT SETCHANGED(supplier(HeaderReception o)) AND
    NOT organizationType(supplier(o)) = OrganizationType.supplier
    CHECKED BY supplier[HeaderReception]
    MESSAGE 'Для приходной накладной организация должна быть поставщиком';
// 6. Формы редактирования и отображения, дизайн, 7. меню
@forms(Reception, supplier, 'Приход', 'Приходы', 'Поставщик'){
    // форма редактирования
    FORM editReception 'Приход'
        OBJECTS h = HeaderReception PANEL // редактирование заголовка
        PROPERTIES (h) number, date
        PROPERTIES (h) organizationName ON CHANGE {
            DIALOG listOrganization OBJECTS o = supplier(h) CHANGE DO supplier(h) <- o;
        }
        OBJECTS r = Reception // редактирование строк
        PROPERTIES (r) nameProduct, nameUnit, quantity, price
        PROPERTIES (r) NEW, DELETE
        EDIT HeaderReception OBJECT h
        FILTERS header(r) = h
    ;
    // подправляем заголовок свойства для разных форм редактирования
    DESIGN editReception { PROPERTY (organizationName(h)) {caption = 'Поставщик';} }
    // форма отображения
    FORM viewReception 'Приходы'
        OBJECTS h = HeaderReception LAST
        PROPERTIES (h) READONLY number, date, organizationName, userName, userDate
        PROPERTIES (h) NEWSESSION NEW, EDIT, DELETE
        OBJECTS r = Reception
        PROPERTIES (r) READONLY nameProduct, nameUnit, quantity, price
        FILTERS header(r) = h
        ORDERS date(h), number(h)
    ;
    // панель кнопок редактирования шапки передвигаем в панель редактирования строк
    // и ограничиваем ширину колонки единиц измерения
    DESIGN viewReception {
        TOOLBARRIGHT (r) { MOVE TOOLBARRIGHT (h); }
        PROPERTY (nameUnit(r)) {charWidth=10;}
    }
    NAVIGATOR {
        documents {
            NEW FORM viewReception;
        }
    }
};

// Расчет итогов
@totalValues(Reception){
    index 'Nпп' (Reception r) = PARTITION SUM 1 ORDER r BY header(r) CHARWIDTH 3;
    cost 'Стоимость' (Reception r) = NUMERIC[10,2](round(quantity(r) * price(r),2)); // по строке
    amount 'Сумма' (HeaderReception h) = GROUP SUM cost(Reception r) IF header(r) = h;
    count 'Количество' (HeaderReception h) = GROUP SUM quantity(Reception r) IF header(r) = h;
    cntpos 'Всего позиций' (HeaderReception h) = GROUP SUM 1 IF header(Reception r) = h;
    amountDay 'Сумма' (HeaderReception h) = PARTITION SUM(amount(h)) BY date(h);
    
    // форма отображения
    EXTEND FORM viewReception
        PROPERTIES (h) READONLY amount AFTER organizationName(h)
        PROPERTIES (r) READONLY index FIRST, cost
        OBJECTS h2 = HeaderReception PANEL // информационное табло
        PROPERTIES(h2) READONLY amount, count, cntpos, amountDay, date
        FILTERS h2 = h;
    // исправляем дизайн: итоги по документу, итоги за день
    DESIGN viewReception {
        PANEL {
            NEW pane {
                horizontal = TRUE;
                fill = 1;
                NEW totalCurrent {
                    horizontal = TRUE;
                    caption = 'Итоги по документу';
                    MOVE PROPERTY (amount(h2)) { background = RGB(204, 204, 255); }
                    MOVE PROPERTY (count(h2)) { background = RGB(204, 204, 255); }
                    MOVE PROPERTY (cntpos(h2)) { background = RGB(204, 204, 255); }
                }
                NEW totalDay {
                    horizontal = TRUE;
                    caption = 'Итоги за день';
                    MOVE PROPERTY (date(h2)) { background = RGB(204, 255, 204); }
                    MOVE PROPERTY (amountDay(h2)) {
                        background = RGB(204, 255, 204);
                        marginLeft = 20;
                    }
                }
            }
        }
    }
};

// Пользовательские фильтры
@filters(Reception, supplier, 'Поставщик'){
    fReceptionNumber
    'Номер' = DATA LOCAL STRING[10];
    fReceptionDate
    'Дата' = DATA LOCAL DATE;
    fReceptionAmount
    'Сумма' = DATA LOCAL INTEGER; // суммы без копеек
    supplier 'ID организации' = DATA LOCAL Organization ();
    fsupplierName 'Поставщик' = name(supplier());
    
    CONSTRAINT SETCHANGED(supplier()) AND NOT organizationType(supplier()) =
        OrganizationType.supplier
        CHECKED BY supplier[] MESSAGE 'Ограничение на тип организации';
    
    onReceptionClear 'Очистить' () {
        fReceptionNumber() <- NULL;
        fReceptionDate() <- NULL;
        fReceptionAmount() <- NULL;
        supplier() <- NULL;
    }
    EXTEND FORM viewReception
        PROPERTIES() onReceptionClear, fReceptionNumber,
            fReceptionDate, fReceptionAmount, fsupplierName
        FILTERS NOT fReceptionNumber() OR number(h) = fReceptionNumber(),
            NOT fReceptionDate() OR date(h) = fReceptionDate(),
            NOT fReceptionAmount() OR floor(amount(h)) = fReceptionAmount(),
            NOT fsupplierName() OR organizationName(h) = fsupplierName()
    ;

    DESIGN viewReception {
        PANEL {
            NEW allPane {
                horizontal = FALSE ;
                tabbed = TRUE;
                MOVE pane {caption = 'Итоги';}
                NEW filters {
                    caption = 'Фиксированные фильтры';
                    horizontal = TRUE;
                    MOVE PROPERTY (onReceptionClear()) {background= RGB(204, 204, 255);};
                    MOVE PROPERTY (fReceptionNumber());
                    MOVE PROPERTY (fReceptionDate());
                    MOVE PROPERTY (fsupplierName());
                    MOVE PROPERTY (fReceptionAmount());
                }
            }
        }
    }
    // ФИЛЬТР по отмеченным
    EXTEND FORM viewReception PROPERTIES(h) mark FIRST;
    AFTER onReceptionClear() DO mark(HeaderReception o) <- NULL;

    // Группа фильтров
    EXTEND FORM viewReception FILTERGROUP group FILTER 'Отмеченные' mark(h);
    EXTEND FORM viewReception EXTEND FILTERGROUP group FILTER 'Больше 300 руб.' amount(h) > 300;
    DESIGN viewReception { filters { MOVE FILTERGROUP (group) LAST; } }
};

