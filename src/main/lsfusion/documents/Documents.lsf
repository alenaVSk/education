MODULE Documents;

REQUIRE Time, Authentication, Organization;

CLASS ABSTRACT Header 'Заголовки документа';

number 'Номер' = DATA STRING[10] (Header) NONULL;
date 'Дата' = DATA DATE (Header) NONULL;
userDate 'Дата ввода' = DATA DATE (Header);
userName 'Пользователь' = DATA STRING[50] (Header);

// автозаполнение
WHEN SET (Header o IS Header) AND NOT userDate(o) DO userDate(o) <- currentDate();
WHEN SET (Header o IS Header) AND NOT userName(o)
    DO userName(o) <- STRING[50](currentUserName());

// новое: определяем уникальность документа
unique = GROUP AGGR Header o BY number(o), date(o);

CLASS ABSTRACT Row 'Строки документа';

quantity 'Количество' = DATA NUMERIC[10,3] (Row) NONULL;
// новое: связываем строки с шапкой
header 'ID шапки' = DATA Header (Row) NONULL DELETE;

// контроль данных
CONSTRAINT quantity(Row o) > 1000 OR quantity(o) <= 0 MESSAGE 'Недопустимое количество';

META forms(object, title1, title2, title3)
    // форма редактирования
    FORM edit##object title1
        OBJECTS h = Header##object PANEL // редактирование заголовка
        PROPERTIES (h) number, date, organizationName
        OBJECTS r = object // редактирование строк
        PROPERTIES (r) name, quantity, price
        PROPERTIES (r) NEW, DELETE
        EDIT Header##object OBJECT h
        FILTERS header(r) = h
    ;
    // подправляем заголовок свойства для разных форм редактирования
    DESIGN edit##object { PROPERTY (organizationName(h)) {caption = title3;} }
    // форма отображения
    FORM view##object title2
        OBJECTS h = Header##object LAST
        PROPERTIES (h) READONLY number, date, organizationName, userName, userDate
        PROPERTIES (h) NEWSESSION NEW, EDIT, DELETE
        OBJECTS r = object
        PROPERTIES (r) READONLY name, quantity, price
        FILTERS header(r) = h
        ORDERS date(h), number(h)
    ;
    // панель кнопок редактирования шапки передвигаем в панель редактирования строк
    DESIGN view##object { TOOLBARRIGHT (r) { MOVE TOOLBARRIGHT (h); }}
    NAVIGATOR {
        documents {
            NEW FORM view##object;
        }
    }
END


