MODULE Documents;

REQUIRE Time, Authentication, Organization;

CLASS ABSTRACT Header 'Заголовки документа';

number 'Номер' = DATA STRING[10] (Header) NONULL;
date 'Дата' = DATA DATE (Header) NONULL;
userDate 'Дата ввода' = DATA DATE (Header);
userName 'Пользователь' = DATA STRING[50] (Header);

// автозаполнение
WHEN SET (Header o IS Header) AND NOT userDate(o) DO userDate(o) <- currentDate();
WHEN SET (Header o IS Header) AND NOT userName(o)
    DO userName(o) <- STRING[50](currentUserName());

// новое: определяем уникальность документа
unique = GROUP AGGR Header o BY number(o), date(o);

CLASS ABSTRACT Row 'Строки документа';

quantity 'Количество' = DATA NUMERIC[10,3] (Row) NONULL;
// новое: связываем строки с шапкой
header 'ID шапки' = DATA Header (Row) NONULL DELETE;

// контроль данных
CONSTRAINT quantity(Row o) > 1000 OR quantity(o) <= 0 MESSAGE 'Недопустимое количество';

// ДОРАБОТКА: отметка записи
mark 'Отметка' = DATA LOCAL BOOLEAN (Header) CHARWIDTH 8;

META forms(object, object2, title1, title2, title3)
    // форма редактирования
    FORM edit##object title1
        OBJECTS h = Header##object PANEL // редактирование заголовка
        PROPERTIES (h) number, date
        PROPERTIES (h) organizationName ON CHANGE {
            DIALOG listOrganization OBJECTS o = object2(h) CHANGE DO object2(h) <- o;
        }
        OBJECTS r = object // редактирование строк
        PROPERTIES (r) nameProduct, nameUnit, quantity, price
        PROPERTIES (r) NEW, DELETE
        EDIT Header##object OBJECT h
        FILTERS header(r) = h
    ;
    // подправляем заголовок свойства для разных форм редактирования
    DESIGN edit##object { PROPERTY (organizationName(h)) {caption = title3;} }
    // форма отображения
    FORM view##object title2
        OBJECTS h = Header##object LAST
        PROPERTIES (h) READONLY number, date, organizationName, userName, userDate
        PROPERTIES (h) NEWSESSION NEW, EDIT, DELETE
        OBJECTS r = object
        PROPERTIES (r) READONLY nameProduct, nameUnit, quantity, price
        FILTERS header(r) = h
        ORDERS date(h), number(h)
    ;
    // панель кнопок редактирования шапки передвигаем в панель редактирования строк
    // и ограничиваем ширину колонки единиц измерения
    DESIGN view##object {
        TOOLBARRIGHT (r) { MOVE TOOLBARRIGHT (h); }
        PROPERTY (nameUnit(r)) {charWidth=10;}
    }
    NAVIGATOR {
        documents {
            NEW FORM view##object;
        }
    }
END;

META totalValues(object)
    index 'Nпп' (object r) = PARTITION SUM 1 ORDER r BY header(r) CHARWIDTH 3;
    cost 'Стоимость' (object r) = NUMERIC[10,2](round(quantity(r) * price(r),2)); // по строке
    amount 'Сумма' (Header##object h) = GROUP SUM cost(object r) IF header(r) = h;
    count 'Количество' (Header##object h) = GROUP SUM quantity(object r) IF header(r) = h;
    cntpos 'Всего позиций' (Header##object h) = GROUP SUM 1 IF header(object r) = h;
    amountDay 'Сумма' (Header##object h) = PARTITION SUM(amount(h)) BY date(h);
    
    // форма отображения
    EXTEND FORM view##object
        PROPERTIES (h) READONLY amount AFTER organizationName(h)
        PROPERTIES (r) READONLY index FIRST, cost
        OBJECTS h2 = Header##object PANEL // информационное табло
        PROPERTIES(h2) READONLY amount, count, cntpos, amountDay, date
        FILTERS h2 = h;
    // исправляем дизайн: итоги по документу, итоги за день
    DESIGN view##object {
        PANEL {
            NEW pane {
                horizontal = TRUE;
                fill = 1;
                NEW totalCurrent {
                    horizontal = TRUE;
                    caption = 'Итоги по документу';
                    MOVE PROPERTY (amount(h2)) { background = RGB(204, 204, 255); }
                    MOVE PROPERTY (count(h2)) { background = RGB(204, 204, 255); }
                    MOVE PROPERTY (cntpos(h2)) { background = RGB(204, 204, 255); }
                }
                NEW totalDay {
                    horizontal = TRUE;
                    caption = 'Итоги за день';
                    MOVE PROPERTY (date(h2)) { background = RGB(204, 255, 204); }
                    MOVE PROPERTY (amountDay(h2)) {
                        background = RGB(204, 255, 204);
                        marginLeft = 20;
                    }
                }
            }
        }
    }
END;

META filters(object1, object2, title1)
    f##object1##Number
    'Номер' = DATA LOCAL STRING[10];
    f##object1##Date
    'Дата' = DATA LOCAL DATE;
    f##object1##Amount
    'Сумма' = DATA LOCAL INTEGER; // суммы без копеек
    object2 'ID организации' = DATA LOCAL Organization ();
    f##object2##Name title1 = name(object2());
    
    CONSTRAINT SETCHANGED(object2()) AND NOT organizationType(object2()) =
        OrganizationType.object2
        CHECKED BY object2[] MESSAGE 'Ограничение на тип организации';
    
    on##object1##Clear 'Очистить' () {
        f##object1##Number() <- NULL;
        f##object1##Date() <- NULL;
        f##object1##Amount() <- NULL;
        object2() <- NULL;
    }
    EXTEND FORM view##object1
        PROPERTIES() on##object1##Clear, f##object1##Number,
            f##object1##Date, f##object1##Amount, f##object2##Name
        FILTERS NOT f##object1##Number() OR number(h) = f##object1##Number(),
            NOT f##object1##Date() OR date(h) = f##object1##Date(),
            NOT f##object1##Amount() OR floor(amount(h)) = f##object1##Amount(),
            NOT f##object2##Name() OR organizationName(h) = f##object2##Name()
    ;

    DESIGN view##object1 {
        PANEL {
            NEW allPane {
                horizontal = FALSE ;
                tabbed = TRUE;
                MOVE pane {caption = 'Итоги';}
                NEW filters {
                    caption = 'Фиксированные фильтры';
                    horizontal = TRUE;
                    MOVE PROPERTY (on##object1##Clear()) {background= RGB(204, 204, 255);};
                    MOVE PROPERTY (f##object1##Number());
                    MOVE PROPERTY (f##object1##Date());
                    MOVE PROPERTY (f##object2##Name());
                    MOVE PROPERTY (f##object1##Amount());
                }
            }
        }
    }
    // ФИЛЬТР по отмеченным
    EXTEND FORM view##object1 PROPERTIES(h) mark FIRST;
    AFTER on##object1##Clear() DO mark(Header##object1 o) <- NULL;

    // Группа фильтров
    EXTEND FORM view##object1 FILTERGROUP group FILTER 'Отмеченные' mark(h);
    EXTEND FORM view##object1 EXTEND FILTERGROUP group FILTER 'Больше 300 руб.' amount(h) > 300;
    DESIGN view##object1 { filters { MOVE FILTERGROUP (group) LAST; } }
END
